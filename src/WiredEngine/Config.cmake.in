include(CMakeFindDependencyMacro)

# Compute the installation prefix relative to this file.
get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
if(_IMPORT_PREFIX STREQUAL "/")
  set(_IMPORT_PREFIX "")
endif()

######
# Exported configuration
######

# Whether Wired was built as shared libs or not
set(WIRED_BUILT_SHARED @BUILD_SHARED_LIBS@)

# Export filepath directories of lib and bin dirs
set(WIRED_INSTALL_LIB_DIR "${_IMPORT_PREFIX}/@CMAKE_INSTALL_LIBDIR@")
set(WIRED_INSTALL_BIN_DIR "${_IMPORT_PREFIX}/@CMAKE_INSTALL_BINDIR@")

# Export that WiredEngine was found
set(WIREDENGINE_FOUND TRUE)

# Export a namespace qualified list of the wired engine library targets
list(APPEND WIREDENGINE_DESKTOP_LIBRARIES
    Wired::NEONCommon
    Wired::WiredGPUVk
    Wired::WiredPlatformSDL
    Wired::WiredEngine
    Wired::WiredDesktop
    Wired::Renderer
)

######
# Dependencies
######

# Dependencies the client needs when linking against a static build of Wired. These are static transitive dependencies
# of Wired. Shared builds of Wired have these built directly into the binaries and thus the client doesn't need to find
# them.
if(NOT WIRED_BUILT_SHARED)
    find_dependency(SDL3 CONFIG REQUIRED)
    find_dependency(unofficial-spirv-reflect CONFIG REQUIRED)
    find_dependency(SDL3_image CONFIG REQUIRED)
    find_dependency(EnTT CONFIG REQUIRED)
    find_dependency(VulkanHeaders REQUIRED)
    find_dependency(VulkanMemoryAllocator CONFIG REQUIRED)
    find_dependency(assimp CONFIG REQUIRED)
    find_dependency(Jolt CONFIG REQUIRED)
    find_dependency(AudioFile CONFIG REQUIRED)
    find_dependency(OpenAL CONFIG REQUIRED)
    find_dependency(OpenAL CONFIG REQUIRED)
endif()

# Dependencies that are part of the public API that the client needs to take a dependency against, no matter whether
# Wired was built as shared or static libraries.
find_dependency(glm CONFIG REQUIRED)
find_dependency(nlohmann_json CONFIG REQUIRED)
if (@WIRED_OPT_IMGUI@)
    find_dependency(Imgui CONFIG REQUIRED)
    find_dependency(Implot CONFIG REQUIRED)
endif()

######
# Include/Export Wired Engine targets
######

include("${CMAKE_CURRENT_LIST_DIR}/WiredEngineTargets.cmake")
